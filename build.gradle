plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

group = 'com.technicjelle'
version = '1.2.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.13-R0.1-SNAPSHOT'
    implementation 'com.github.BlueMap-Minecraft:BlueMapAPI:v2.2.1'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
}

shadowJar {
    dependencies {
        include(dependency('org.bstats::'))
    }
    relocate 'org.bstats', 'com.technicjelle.bluemapchatmarkers.bstats'
}

def targetJavaVersion = 11
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}